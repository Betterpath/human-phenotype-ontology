PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX hp: <http://purl.obolibrary.org/obo/hp.owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

# Encountered some newlines in names and definitions
SELECT DISTINCT (STR(?term) AS ?IRI) (REPLACE(STR(?label), "\n", " ") AS ?primary_label) (REPLACE(STR(?def), "\n", " ") AS ?definition)
WHERE
{
?term rdfs:label ?label .
OPTIONAL { ?term obo:IAO_0000115 ?def . }
FILTER(STRSTARTS(STR(?term), "http://purl.obolibrary.org/obo/HP_"))
FILTER NOT EXISTS {
?term ?prop ?value .
?axiom owl:annotatedSource ?term .
?axiom owl:annotatedProperty ?prop .
?axiom owl:annotatedTarget ?value .
?axiom hp:layperson ?blah .
}
}